#!/usr/bin/env perl
use strict;
use warnings;

use FindBin qw($RealBin);
use lib "$RealBin/lib";

use Github::Tools::Common;
use Data::Dump;
use Carp;
use Net::HTTP::Spore;
use Try::Tiny;
use Text::SimpleTable::AutoWidth;

use feature ':5.10';
my $table = Text::SimpleTable::AutoWidth->new();

my $teams = list_org_teams();
my @teams = grep {
    defined( $_->{id} )
        and
    ( defined( $_->{name} ) and $_->{name} ne 'Owners' )
} @{ $teams };

{
    foreach my $t (@teams) {
        $t->{repos} = team_repos($t->{id});
    }
}

$table->captions(['Repo',
        map {
            my $m = 11 - length($_);
            my $d = int($m / 2);
            " "x(($m % 2) ? $d + 1 : $d) . $_ . " "x($d)
        }
        map { substr($_->{name}, - 11) }
        @teams
    ]);


iterate_repos(config 'org', sub {
        my $r = shift;
        my @row = ($r->{name});

        foreach my $t (@teams) {
            if (grep { $row[0] eq $_->{name}} @{ $t->{repos}}) {
                push @row, "     X";
            } else {
                push @row, " ";
            }
        }

        $table->row(@row);
});


say $table->draw;
